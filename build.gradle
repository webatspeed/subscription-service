plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'com.webatspeed.subscription'
version = '0.1.0'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    all*.exclude group: 'commons-logging', module: 'commons-logging'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
    constraints {
        implementation('com.amazonaws:aws-java-sdk-s3:1.12.571') {
            because 'version <=1.12.260 pulled from starter-aws has vulnerabilities'
        }
    }
    implementation 'com.amazonaws:aws-java-sdk-sesv2:1.12.571'
    implementation 'com.sun.mail:jakarta.mail:2.0.1'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'net.datafaker:datafaker:2.0.2'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring31x:4.9.3'
    testImplementation 'org.instancio:instancio-junit:3.4.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

bootBuildImage {
    createdDate = "now"
}
